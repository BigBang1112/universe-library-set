/*
// Manialink.Script.txt
// by BigBang1112
// part of Universe Library Set

// Cleaner construction of manialinks by seperating markup and script into two files.

// This library does depend on more libraries from the Universe Library Set:
// - File.Script.txt
// - Dictionary.Script.txt
// You only need to use the libraries above, although it's recommended to use the whole set.
*/

/*
// Compatible contexts:
// - CManiaApp
// - CMode
// - CServerPlugin?
*/

/*
// Text Construct(Text _Markup, Text _Script)
// Text FromFile(Text _ManialinkName, Boolean _Translate)
// Text FromFile(Text _ManialinkName)
*/

#Include "TextLib" as TextLib

#Include "Libs/BigBang1112/File.Script.txt" as File
#Include "Libs/BigBang1112/Dictionary.Script.txt" as Dictionary

Text Construct(Text _Markup, Text _Script, Text[] _Extentions) {
	declare Text ExtensionDirectives;
	declare Text ExtensionGlobals;
	declare Text ExtensionStarts;
	declare Text ExtensionLoops;
	
	foreach(Extension,_Extentions) {
		ExtensionDirectives ^= File::Read("Media/Extensions/"^Extension^".Directives.Script.txt")^"\n\n";
		ExtensionGlobals ^= File::Read("Media/Extensions/"^Extension^".Global.Script.txt")^"\n\n";
		ExtensionStarts ^= File::Read("Media/Extensions/"^Extension^".Start.Script.txt")^"\n\n";
		ExtensionLoops ^= File::Read("Media/Extensions/"^Extension^".Loop.Script.txt")^"\n\n";
	}

	return """<manialink version="3">
			{{{_Markup}}}
			<script><![CDATA[#Include "TextLib" as TextLib
#Include "MathLib" as MathLib
#Include "AnimLib" as AnimLib

{{{ExtensionDirectives}}}

{{{ExtensionGlobals}}}
{{{_Script}}}

Void Main() {
	{{{ExtensionStarts}}}
	+++Start+++
	while(True) {
		{{{ExtensionLoops}}}
		+++Loop+++
		yield;
	}
}

main() {
	Main();
}]]></script>
	</manialink>""";
}

Text Construct(Text _Markup, Text _Script) {
	return Construct(_Markup, _Script, []);
}

Text FromFile(Text _ManialinkName, Boolean _Translate, Text[] _Extensions) {
	declare Markup = File::Read(_ManialinkName^".xml");
	declare Script = File::Read(_ManialinkName^".Script.txt");
	
	if(_Translate) {
		Markup = Dictionary::TranslateAllPhrases(Markup);
		Script = Dictionary::TranslateAllPhrases(Script);
	}

	return Construct(Markup, Script, _Extensions);
}

Text FromFile(Text _ManialinkName, Boolean _Translate) {
	return FromFile(_ManialinkName, _Translate, []);
}